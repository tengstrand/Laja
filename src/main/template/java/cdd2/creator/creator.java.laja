#namespace creator

#macro initializeCreator()
  #set (filename = creator.filename)
  #set (packagename = creator.packagename)
  #set (packageStatement = creator.packageStatement)
  #set (imports = creator.imports)
  #set (classname = creator.classname)
  #set (statevariable = creator.statevariable)
  #set (asMethods = creator.asMethods)
  #set (parameters = creator.parameters)
  #set (isGeneratedCode = creator.isGeneratedCode)
  #set (state = creator.state)
  #set (immutableState = "{state}State")
  #set (mutableState = "{state}MutableState")
  #set (manualCode = creator.manualCode)
  #set (mutableState = "{state}MutableState")
  #set (factory = "{state}Factory")
  #set (builder = creator.builder)
  #set (maker = "{creator.maker}")
  #set (creator = "{creator.classname}")
  #set (listBuilder = "{state}ListBuilder")
  #set (setBuilder = "{state}SetBuilder")
  #set (mapBuilder = "{state}MapBuilder")
  #set (mapEntryBuilder = "{state}MapEntryBuilder")
  #set (superBehaviour = "{state}Behaviour")
  #set (superMutableBehaviour = "{state}MutableBehaviour")

  #set (firstMandatory = null)
  #set (secondMandatory = null)
  #set (mandatoryAttributes = new java.util.ArrayList())
  #foreach (attribute in attributes where attribute.isMandatory)
    #if (firstMandatory != null && secondMandatory == null)
      #set (secondMandatory = attribute)
    #end
    #mandatoryAttributes.add(attribute)
    #if (firstMandatory == null)
      #set (firstMandatory = attribute)
    #end
  #end
  #set (hasMandatoryAttributes = mandatoryAttributes.size > 0)
#end

#macro imports()
  #if (!isGeneratedCode)
import com.google.common.collect.ImmutableList;
import com.google.common.collect.ImmutableMap;
import com.google.common.collect.ImmutableSet;
import net.sf.laja.cdd.annotation.Creator;
import net.sf.laja.cdd.validator.ValidationErrors;
import net.sf.laja.cdd.validator.Validator;
import org.joda.time.*;
import java.util.*;

    #set (stateImports = new java.util.TreeSet())
    #set (staticStateImports = new java.util.TreeSet())
    #stateImports.add("{statePackagename}.{immutableState}")
    #foreach (attribute in attributes where attribute.isState)
      #stateImports.add("{attribute.stateObject.packagename}.{attribute.asImmutable.type}.{attribute.asMutable.type}")
      #staticStateImports.add("{attribute.creator.packagename}.{attribute.creator.classname}.*")
    #end
    #foreach (import in stateImports)
import {import};
    #end

    #foreach (import in staticStateImports)
import static {import};
    #end

  #end
{imports}##
#end

#macro fields()
  #if (!isGeneratedCode)

    // ===== Generated code =====
  #else
// ===== Generated code =====
  #end

  #foreach(attribute in attributes)
    private static final String {attribute.name}_ = "{attribute.name}";
  #end

#end

#macro create()
    // --- Constructors ---

  #set (methodName = constructorMethodName("create"))
  #if (hasMandatoryAttributes)
    public static {factory} {methodName}() {
        return new {creator}(new {mutableState}()).new {factory}();
    }
  #else
    public static {creator} {methodName}() {
        return new {creator}(new {mutableState}());
    }
  #end

#end

#macro build()
  #set (methodName = constructorMethodName("build"))
    public static {builder} {methodName}() {
        return {builder}.create();
    }

#end

#macro createList()
  #set (methodName = constructorMethodName("create"))
    public static {listBuilder} {methodName}List({creator}... creators) {
        return new {listBuilder}(creators);
    }

#end

#macro createSet()
  #set (methodName = constructorMethodName("create"))
    public static {setBuilder} {methodName}Set({creator}... creators) {
        return new {setBuilder}(creators);
    }

#end

#macro createMap()
  #set (methodName = constructorMethodName("create"))
    public static {mapBuilder} {methodName}Map({mapEntryBuilder}... builders) {
        return new {mapBuilder}(builders);
    }

#end

#macro mapCreatorEntry()
  #set (methodName = constructorMethodName(""))
    public static {mapEntryBuilder} {methodName}Entry(Object key, {creator} creator) {
        return new {mapEntryBuilder}(key, creator);
    }

#end

#macro mapBuilderEntry()
  #set (methodName = constructorMethodName(""))
    public static {mapEntryBuilder} {methodName}Entry(Object key, {builder} builder) {
        return new {mapEntryBuilder}(key, builder);
    }

#end

#function constructorMethodName(action)
  #if (asMethods.size > 0)
    #return (asMethods.get(0).actionName(action))
  #else
    #return "{action}{state}"
  #end

#end

#macro constructor()

    // --- Constructor ---

    public {creator}({mutableState} state) {
        this.state = state;
    }

#end

#macro asState()
    public {immutableState} asState() {
        return state.asImmutable();
    }

#end

#macro asMutableState()
    public {mutableState} asMutableState() {
        return state;
    }

#end

#macro withMethods()
  #if (!attributes.isEmpty)
    // --- With methods ---

  #end
  #foreach (attribute in attributes where attribute.isOptionalOrId)
    public {creator} {attribute.withMethod}({attribute.type} {attribute.name}) {
        state.{attribute.name} = {attribute.name};
        return this;
    }

    #if (attribute.isDate)
    public {creator} {attribute.withMethod}(int year, int monthOfYear, int dayOfMonth) {
        state.{attribute.name} = new {attribute.type}(year, monthOfYear, dayOfMonth);
        return this;
    }

    #end
    #if (attribute.isCollectionOrMapWithState)
      #set (leafState = attribute.leafState)
      #set (type = attribute.collectionOrMapType)
    public {creator} {attribute.withMethod}({leafState}{type}Builder {attribute.name}) {
        state.{attribute.name} = {attribute.name}.asMutableState{type}();
        return this;
    }

    #end
    #foreach (parameter in parameters where parameter.name == attribute.name && parameter.isParameterMethod && !parameter.value.isEmpty && parameter.useNext)
    public {creator} {attribute.withMethod}({parameter.signature}) {
        state.{attribute.name} = {parameter.value};
        return this;
    }

    #end
  #end
#end

#macro assertIsValid()
    // --- Validate ---

    public void assertIsValid() {
        state.assertIsValid();
    }

#end

#macro isValid()
    public boolean isValid() {
        return state.isValid();
    }

#end

#macro validate()
    public ValidationErrors validate(Validator... validators) {
        return state.validate(validators);
    }

#end

#import "{.}/factory.java.laja"
#import "{.}/superBehaviour.java.laja"
#import "{.}/superMutableBehaviour.java.laja"
#import "{.}/builder.java.laja"
#import "{.}/listBuilder.java.laja"
#import "{.}/setBuilder.java.laja"
#import "{.}/mapBuilder.java.laja"
#import "{.}/mapEntryBuilder.java.laja"
#import "{.}/maker.java.laja"

#macro creatorContent()
package {packagename};

    #imports()
@Creator
public class {classname} implements {maker} {
    private final {mutableState} state;##
    #foreach (method in asMethods)
{method.spaces}public {method.returnclass} {method.methodName}({method.parameters}) {method.body}##
    #end
{parameters.content}##
{manualCode}##
    #fields()
    #create()
    #build()
    #createList()
    #createSet()
    #createMap()
    #mapCreatorEntry()
    #mapBuilderEntry()
    #factory()
    #constructor()
    #asState()
    #asMutableState()
    #withMethods()
    #assertIsValid()
    #isValid()
    #validate()
    #superBehaviour()
    #superMutableBehaviour()
    #builder()
    #listBuilder()
    #setBuilder()
    #mapEntryBuilder()
    #mapBuilder()
}
    #maker()
#end

#macro generateCreator(c)
  #set (creator = c)
  #initializeCreator()

  #generateFile(filename, creatorContent())
#end
