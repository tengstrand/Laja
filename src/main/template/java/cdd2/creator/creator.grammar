grammar creator {
   newline = "\r\n" | "\n";
   comment = ("/*" [(comment | !"*/")+] "*/") | ("//" [!newline+] newline|END);
   ws = (newline | " " | "\t" | comment)+;
   s = [ws];
   ff = "\"";
   str = ff (("\\\"" | !(ff|newline))+):strval ff;

   letter = "a".."z" | "A".."Z" | "_" | "$";
   digit = "0".."9";
   name = letter [letter | digit]+;

   public = ["public" ws];
   packagename = name ["." name|"*"]+;
   packageStatement = "package" ws packagename s ";";
   fullclassname = packagename;

   importstatement = "import" ws ["static":static ws] fullclassname s ";" s;
   imports = [importstatement+];

   body = "{" s [(body | str | !"}")+]:innerStatement "}";
   parameter = s name:type ws name:variable s;
   parameters = [parameter] ["," s parameter]+;

   asMethod = s:spaces "public" s name:returnclass ws ("as" name):methodName s "(" s parameters s ")" s body;

   aparameterAttr = name:variable s "=" s (name|str):value;
   aparameter = "@Parameter" s "(" s aparameterAttr [s "," s aparameterAttr]+ s ")";
   aparameters = s "@Parameters" s "(" s "{" s aparameter [s "," s aparameter]+ s "}" s ")";

   keywords = ["private"|"public"|"static"|"final" s]+;
   annotation = "@Creator" [s "(" ["main":main] s ")"];
   classStatement = annotation ws public "class" ws name:classname [s "implements" s name [s "," s name]+] s "{" s
                    keywords s name:stateClass s "state;" [asMethod|aparameters]+;

   manualEnd = ("}" s END);
   generatedCode = "// ===== Generated code =====";
   manualCode = !(manualEnd | generatedCode)+;

   creator = packageStatement s imports s classStatement manualCode [generatedCode] *;

   PackageStatement packageStatement.setPackagename(String packagename);
   packageStatement.setContent(String *);

   Parameter parameter.setType(String type);
   parameter.setVariable(String variable);

   Parameters parameters.addParameter(Parameter parameter);

   AsMethod asMethod.setSpaces(String spaces);
   asMethod.setReturnclass(String returnclass);
   asMethod.setMethodName(String methodName);
   asMethod.setParameters(Parameters parameters);
   asMethod.setBody(String body);

   AparameterAttr aparameterAttr.setVariable(String variable);
   aparameterAttr.setValue(String value);

   Aparameter aparameter.addParameterAttr(AparameterAttr aparameterAttr);

   Aparameters aparameters.addParameter(Aparameter aparameter);
   aparameters.setContent(String *);

   ClassStatement classStatement.setClassname(String classname);
   classStatement.setStateClass(String stateClass);
   classStatement.addAsMethod(AsMethod asMethod);
   classStatement.setAParameters(Aparameters aparameters);

   Creator creator.setPackageStatement(PackageStatement packageStatement);
   creator.setImports(String imports);
   creator.setClassStatement(ClassStatement classStatement);
   creator.setManualCode(String manualCode);
   creator.setGeneratedCode(void generatedCode);
   creator.setIsMainCreator(void annotation.main);
}
