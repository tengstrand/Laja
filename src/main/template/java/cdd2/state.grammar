grammar state {
   newline = "\r\n" | "\n";
   comment = ("/*" [(comment | !"*/")+] "*/") | ("//" [!newline+] newline|END);
   ws = (newline | " " | "\t" | comment)+;
   s = [ws];
   ff = "\"";
   str = ff (("\\\"" | !(ff|newline))+):strval ff;

   letter = "a".."z" | "A".."Z" | "_" | "$";
   digit = "0".."9";
   name = letter [letter | digit]+;

   public = ["public" ws];
   packagestatement = "package" ws packagename s ";";
   packagename = name ["." name|"*"]+;
   fullclassname = packagename;

   importstatement = "import" ws ["static":static ws] fullclassname s ";" s;
   imports = [importstatement+];

   annotations = [("@" name s):annotation]+;
   keywords = ["public "|"private "|"final " s]+;
   type = name [s "<" s type s ">"] [s "," s type];
   attribute = s annotations keywords s type ws name:variable s ";" [!newline+] newline;

   uname = "A".."Z" [letter | digit]+;
   getter = "get" uname s "(" s ")";

   body = "{" s [(body | str | !"}")+]:innerStatement "}";

   manualEnd = ("}" s END);
   generatedEnd = ("// Generated" *);
   manualCode = !(manualEnd | generatedEnd)+;

   classStatement = "@State" ws public "class" ws name:classname s ["extends" ws name s]
                    ["implements" ws name [s "," s name]+ s] "{" s [attribute]+ manualCode
                    manualEnd|generatedEnd *;

   state = s packagestatement s imports s classStatement s;

   Importstatement importstatement.setStatement(String *);
   importstatement.setStatic(void static);
   importstatement.setFullclassname(String fullclassname);

   Imports imports.addImportstatement(Importstatement importstatement);

   Attribute attribute.setAnnotations(String annotations);
   attribute.addAnnotation(String annotations.annotation);
   attribute.setType(String type);
   attribute.setVariable(String variable);

   ClassStatement classStatement.setClassname(String classname);
   classStatement.addAttribute(Attribute attribute);
   classStatement.setManualCode(String manualCode);
   classStatement.setGeneratedEnd(void generatedEnd);

   State state.setPackagename(String packagestatement.packagename);
   state.setImports(Imports imports);
   state.setClassStatement(ClassStatement classStatement);
}
