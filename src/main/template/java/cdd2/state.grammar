grammar state {
   newline = "\r\n" | "\n";
   comment = ("/*" [(comment | !"*/")+] "*/") | ("//" [!newline+] newline|END);
   ws = (newline | " " | "\t" | comment)+;
   s = [ws];
   ff = "\"";
   str = ff (("\\\"" | !(ff|newline))+):strval ff;

   letter = "a".."z" | "A".."Z" | "_" | "$";
   digit = "0".."9";
   name = letter [letter | digit]+;

   public = ["public" ws];
   packagestatement = "package" ws packagename s ";";
   packagename = name ["." name|"*"]+;
   fullclassname = packagename;

   importstatement = "import" ws ["static":static ws] fullclassname s ";" s;
   imports = [importstatement+];

   annotations = [("@" name s):annotation+];
   keywords = ["public "|"private "|"final " s]+;
   collectionType = s "<" s type s ">";
   mapType = s "<" s type:key s "," s type:entry s ">";
   type = name [collectionType | mapType];
   attribute = s annotations keywords s type ws name:variable s ";" [!newline+] newline;

   manualEnd = ("}" s END);
   generatedEnd = ("//" s  ["*"+ | "-"+ | "="+] s "Generated" [!newline+]):generatedText *;
   manualCode = !(manualEnd | generatedEnd)+;

   classStatement = "@State" ws public "class" ws name:classname s ["extends" ws name s]
                    ["implements" ws name [s "," s name]+ s] "{" s [attribute]+ manualCode
                    manualEnd|generatedEnd *;

   state = s packagestatement s imports s classStatement s;

   Importstatement importstatement.setStatement(String *);
   importstatement.setStatic(void static);
   importstatement.setFullclassname(String fullclassname);

   Imports imports.addImportstatement(Importstatement importstatement);

   CollectionType collectionType.setType(Type type);

   MapType mapType.setKey(Type key);
   mapType.setEntry(Type entry);

   Type type.setName(String name);
   type.setCollectionType(CollectionType collectionType);
   type.setMapType(MapType mapType);

   Annotations annotations.addAnnotation(String annotation);
   annotations.setContent(String *);

   Attribute attribute.setAnnotations(Annotations annotations);
   attribute.setType(Type type);
   attribute.setVariable(String variable);

   ClassStatement classStatement.setClassname(String classname);
   classStatement.addAttribute(Attribute attribute);
   classStatement.setManualCode(String manualCode);
   classStatement.setGeneratedText(String generatedEnd.generatedText);

   State state.setPackagename(String packagestatement.packagename);
   state.setImports(Imports imports);
   state.setClassStatement(ClassStatement classStatement);
}
