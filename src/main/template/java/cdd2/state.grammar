grammar state {
   newline = "\r\n" | "\n";
   comment = ("/*" [(comment | !"*/")+] "*/") | ("//" [!newline+] newline|END);
   ws = (newline | " " | "\t" | comment)+;
   s = [ws];
   ff = "\"";
   str = ff (("\\\"" | !(ff|newline))+):strval ff;

   letter = "a".."z" | "A".."Z" | "_" | "$";
   digit = "0".."9";
   name = letter [letter | digit]+;

   public = ["public" ws];
   packagename = name ["." name|"*"]+;
   packagestatement = "package" ws packagename s ";";
   fullclassname = packagename;

   importstatement = "import" ws ["static":static ws] fullclassname s ";" s;
   imports = [importstatement+];

   value = str | (!(ws | ")" | ",")+);
   annotationAttribute = name s "=" s value;
   annotation = "@" name s ["(" s annotationAttribute [s "," s annotationAttribute]+ s ")" s];
   annotations = [annotation]+;
   keywords = ["public "|"private "|"final " s]+;
   collectionType = s "<" s type s ">";
   mapType = s "<" s type:key s "," s type:entry s ">";
   type = name [collectionType | mapType];
   attribute = s annotations s keywords s type ws name:variable s ";" [!newline+] newline;

   manualEnd = ("}" s END);
   generatedEnd = "//" s  ["*"+ | "-"+ | "="+] s "Generated" [!newline+];
   manualCode = !(manualEnd | generatedEnd)+;

   version = digit+;
   classStatement = "@State" [s "(" s "version" s "=" s version s ")"] ws public "class" ws name:classname s ["extends" ws name s]
                    ["implements" ws name [s "," s name]+ s] "{" s [attribute]+ manualCode
                    manualEnd|generatedEnd *;

   state = s packagestatement s imports s classStatement s;

   Importstatement importstatement.setStatement(String *);
   importstatement.setStatic(void static);
   importstatement.setFullclassname(String fullclassname);

   Imports imports.addImportstatement(Importstatement importstatement);

   CollectionType collectionType.setType(Type type);

   MapType mapType.setKey(Type key);
   mapType.setEntry(Type entry);

   Type type.setName(String name);
   type.setCollectionType(CollectionType collectionType);
   type.setMapType(MapType mapType);

   AnnotationAttribute annotationAttribute.setName(String name);
   annotationAttribute.setValue(String value);

   Annotation annotation.setName(String name);
   annotation.addAnnotationAttribute(AnnotationAttribute annotationAttribute);

   Annotations annotations.addAnnotation(Annotation annotation);
   annotations.setContent(String *);

   Attribute attribute.setAnnotations(Annotations annotations);
   attribute.setType(Type type);
   attribute.setVariable(String variable);

   ClassStatement classStatement.setVersion(String version);
   classStatement.setClassname(String classname);
   classStatement.addAttribute(Attribute attribute);
   classStatement.setManualCode(String manualCode);
   classStatement.setGeneratedEnd(String generatedEnd);

   State state.setPackagename(String packagestatement.packagename);
   state.setImports(Imports imports);
   state.setClassStatement(ClassStatement classStatement);
}
