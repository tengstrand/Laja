
#set (packagename = state.packagename)
#set (classname = state.classname)
#set (classvariable = state.classvariable)
#set (mutableClass = state.mutableClass)
#set (attributes = state.attributes)
#set (imports = state.imports)
#set (manualCode = state.manualCode)
#set (generatedText = state.generatedText)
#set (isEntity = state.isEntity)

#set (attributeConverter = new net.sf.laja.parser.cdd.AttributeToConstantConverter())

#macro imports()
  #foreach (import in imports)
{import.statement}##
  #end
#end

#macro attributes()
  #foreach (attribute in attributes)
    {attribute.annotationsContent}public final {attribute.typeContent} {attribute.variable};
  #end
#end

#macro constants()
  #foreach (attribute in attributes)
    public static final String {attributeConverter.toConstant(attribute.variable)} = "{attribute.variable}";
  #end

#end

#macro constructor()
    public {classname}(
  #set (iterator = attributes.iterator)
  #set (space = " ")
  #while (iterator.hasNext)
    #set (attribute = iterator.next)
    #if (iterator.hasNext)
            {attribute.typeContent} {attribute.variable},
    #else
            {attribute.typeContent} {attribute.variable}) {
    #end
  #end
  #foreach (attribute in attributes)
        this.{attribute.variable} = {attribute.variable};
  #end
    }

#end

#macro exception()
    public static class IllegalPersonStateException extends InvalidStateException {
        public IllegalPersonStateException(ValidationErrors errors) {
            super(errors);
        }
    }

#end

#macro withMethods()
  #set (parameterList = "")
  #set (delimiter = "")
  #foreach (attribute in attributes)
    #set (parameterList = "{parameterList}{delimiter}{attribute.variable}")
    #set (delimiter = ", ")
  #end
  #foreach (attribute in attributes)
    public {classname} with{attribute.variableAsClass}({attribute.typeContent} {attribute.variable}) { return new {classname}({parameterList}); }
  #end

#end

#macro mutableConverters(type)
  #if (type.isState)
, toMutable##
  #else if (type.isMap)
, toMutableMap{mutableConverters(type.mapType.entry)}##
  #else if (type.isList)
, toMutableList{mutableConverters(type.collectionType.type)}##
  #else if (type.isSet)
, toMutableSet{mutableConverters(type.collectionType.type)}##
  #end
#end

#macro mutableStateAttribute()
  #if (attribute.type.isMap)
asMutableMap({attribute.variable}{mutableConverters(attribute.type.mapType.entry)})##
  #else if (attribute.type.isList)
asMutableList({attribute.variable}{mutableConverters(attribute.type.collectionType.type)})##
  #else if (attribute.type.isSet)
asMutableSet({attribute.variable}{mutableConverters(attribute.type.collectionType.type)})##
  #else if (attribute.type.isState)
{attribute.variable}.asMutable()##
  #else
{attribute.variable}##
  #end
#end

#macro asMutable()
    public {mutableClass} asMutableState() {
        return new {mutableClass}(
  #set (iterator = attributes.iterator)
  #while (iterator.hasNext)
    #set (attribute = iterator.next)
    #if (iterator.hasNext)
            {mutableStateAttribute()},
    #else
            {mutableStateAttribute()});
    #end
  #end
    }

#end

#function entityAttributes()
  #set (keys = new java.util.ArrayList())
  #foreach (attribute in attributes where attribute.isKey)
    ## 1. At least one @Key was found => use @Key attributes for identity.
    #keys.add(attribute)
  #end
  #if (keys.size == 0)
    ## No @Key, but one @Id attribute was found.
    #foreach (attribute in attributes where !attribute.isId && !attribute.isCollection && !attribute.isMap)
      ## 2. Use all none @Id/collection/map attributes for identity.
      #keys.add(attribute)
    #end
    #if (keys.size == 0)
      #foreach (attribute in attributes where !attribute.isId)
        ## 3. Use all none @Id attributes for identity.
        #keys.add(attribute)
      #end
      #if (keys.size == 0)
        ## 4. Use the @Id attribute for identity.
        #return attributes
      #end
    #end
  #end
  #return keys
#end

#macro hashCode()
    @Override
    public int hashCode() {
  #if (isEntity)
    #hashCodeBody(entityAttributes())
  #else
    #hashCodeBody(attributes)
  #end
    }

#end

#macro valueHashCode()
  #if (isEntity)
    public int valueHashCode() {
    #hashCodeBody(attributes)
    }

  #end
#end

#macro hashCodeBody(attributes)
  #if (attributes.isEmpty)
        return super.hashCode();
  #else
    #set (first = true)
    #set (intTypes = ["byte", "char", "short", "int"])
    #set (castTypes = ["float", "double"])
    #foreach (attribute in attributes)
      #if (first)
        #if (intTypes.contains(attribute.type))
        int result = {attribute.variable};
        #else if (castTypes.contains(attribute.type))
        int result = (int){attribute.variable};
        #else if (attribute.type == "long")
        int result = (int)({attribute.variable} ^ ({attribute.variable} >>> 32));
        #else if (attribute.type == "boolean")
        int result = {attribute.variable} ? 1 : 0;
        #else
        int result = {attribute.variable} != null ? {attribute.variable}.hashCode() : 0;
        #end
      #else
        #if (intTypes.contains(attribute.type))
        result = 31 * result + {attribute.variable};
        #else if (castTypes.contains(attribute.type))
        result = 31 * result + (int){attribute.variable};
        #else if (attribute.type == "long")
        result = 31 * result + (int)({attribute.variable} ^ ({attribute.variable} >>> 32));
        #else if (attribute.type == "boolean")
        result = 31 * result + ({attribute.variable} ? 1 : 0);
        #else
        result = 31 * result + ({attribute.variable} != null ? {attribute.variable}.hashCode() : 0);
        #end
      #end
      #set (first = false)
    #end

        return result;
  #end
#end

#macro equals()
    @Override
    public boolean equals(Object that) {
  #if (isEntity)
    #equalsBody(entityAttributes())
  #else
    #equalsBody(attributes)
  #end
    }

#end

#macro valueEquals()
  #if (isEntity)
    public boolean valueEquals(Object that) {
    #equalsBody(attributes)
    }

  #end
#end

#macro equalsBody(attributes)
  #if (attributes.isEmpty)
        return super.equals(that);
  #else
        if (this == that) return true;
        if (that == null || getClass() != that.getClass()) return false;

        {classname} state = ({classname})that;

    #foreach (attribute in attributes)
      #if (attribute.isPrimitive)
        if ({attribute.variable} != state.{attribute.variable) return false;
      #else
        if ({attribute.variable} != null ? !{attribute.variable}.equals(state.{attribute.variable}) : state.{attribute.variable} != null) return false;
      #end
    #end

        return true;
  #end
#end




#write "C:/Source/IDEA/Laja/src/testgen/java-new/main/net/sf/laja/cdd/state/PersonState.txt"
package {packagename};

  #imports()
@State
public class {classname} implements ImmutableState {
  #attributes()##
  #if (generatedText != null)
{manualCode}##
{generatedText}
  #else
{manualCode}
    // ===== Generated code =====
  #end

  #constants()
  #constructor()
  #exception()
  #withMethods()
  #asMutable()
  #hashCode()
  #equals()
  #valueHashCode()
  #valueEquals()
}
#end
