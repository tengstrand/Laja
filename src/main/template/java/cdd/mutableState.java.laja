#namespace mutable

#macro classHeader()
  #if (version < 1)
    @State(type = "mutable")
  #else
    @State(version = {version}, type = "mutable")
  #end
    public static class {mutableClass} implements MutableState {
#end

#macro copyConstructor()

        public {mutableClass}({mutableClass} state) {
  #foreach (attribute in attributes)
            {attribute.name} = state.{attribute.name};
  #end
        }
#end

#macro emptyConstructor(emptyConstructorClass)

        public {emptyConstructorClass}() {
  #foreach (attribute in attributes)
    #if (attribute.isOptional && attribute.optional.valueFor("defaultValue") != null)
            {attribute.name} = {attribute.optional.valueFor("defaultValue")};
    #end
  #end
        }
#end

#macro setters()

  #foreach (attribute in attributes)
        public void set{attribute.nameAsClass}({attribute.type} {attribute.name}) { this.{attribute.name} = {attribute.name}; }
  #end
#end

#macro withMethods()

  #foreach (attribute in attributes)
          #if (attribute.type.isStateList)
        public {mutableClass} {attribute.withMethod}() { this.{attribute.name} = new ArrayList<{attribute.type.collectionType.type.asMutable}>(); return this; }
          #else if (attribute.type.isStateSet)
        public {mutableClass} {attribute.withMethod}() { this.{attribute.name} = new HashSet<{attribute.type.collectionType.type.asMutable}>(); return this; }
          #else if (attribute.type.isStateMap)
        public {mutableClass} {attribute.withMethod}() { this.{attribute.name} = new HashMap<{attribute.type.mapType.key},{attribute.type.mapType.entry.asMutable}>(); return this; }
          #end
        public {mutableClass} with{attribute.nameAsClass}({attribute.type} {attribute.name}) { this.{attribute.name} = {attribute.name}; return this; }
  #end
#end

#macro assertIsValid()

        public void assertIsValid() {
            ValidationErrors errors = validate();

            if (errors.isInvalid()) {
                throw new {stateException}(errors);
            }
        }
#end

#macro isValid()

        public boolean isValid() {
            return validate().isValid();
        }
#end

#macro rootValidate()

        public ValidationErrors validate() {
            ValidationErrors.Builder errors = ValidationErrors.builder();
            validate(this, "", errors);
            return errors.build();
        }
#end

#function hasStateOrCollectionOrMap()
  #foreach (attribute in attributes where attribute.isState || attribute.isCollection || attribute.isMap)
    #return true
  #end
  #return false
#end

#macro validate()

        public void validate(Object rootElement, String parent, ValidationErrors.Builder errors) {
  #if (state.hasMandatoryObjectAttribute)
    #foreach (attribute in attributes where attribute.isMandatory && !attribute.isPrimitive)
            if ({attribute.name} == null) errors.addIsNullError(rootElement, "{attribute.name}", parent);
    #end

  #end
  #if (hasStateOrCollectionOrMap())
    #foreach (attribute in attributes where !attribute.isPrimitive)
      #if (attribute.isState)
            if ({attribute.name} != null) {attribute.name}.validate(rootElement, concatenate(parent, "{attribute.name}"), errors);
      #else if (attribute.isCollection && attribute.isLeafState)
            if ({attribute.name} != null) collectionValidator().validate(rootElement, {attribute.name}, parent, "{attribute.name}", errors, 0);
      #else if (attribute.isMap && attribute.isLeafState)
            if ({attribute.name} != null) mapValidator().validate(rootElement, {attribute.name}, parent, "{attribute.name}", errors, 0);
      #end
    #end
  #end
        }
#end

#macro immutableConverters(type)
  #if (type.isState)
, toImmutable##
  #else if (type.isMap)
, toImmutableMap{immutableConverters(type.mapType.entry)}##
  #else if (type.isList)
, toImmutableList{immutableConverters(type.collectionType.type)}##
  #else if (type.isSet)
, toImmutableSet{immutableConverters(type.collectionType.type)}##
  #end
#end

#macro immutableStateAttribute()
  #if (attribute.type.isMap)
asImmutableMap({attribute.name}{immutableConverters(attribute.type.mapType.entry)})##
  #else if (attribute.type.isList)
asImmutableList({attribute.name}{immutableConverters(attribute.type.collectionType.type)})##
  #else if (attribute.type.isSet)
asImmutableSet({attribute.name}{immutableConverters(attribute.type.collectionType.type)})##
  #else if (attribute.type.isState)
{attribute.name} != null ? {attribute.name}.asImmutable() : null##
  #else
{attribute.name}##
  #end
#end

#macro asImmutable()

  #if (attributes.size == 1)
    #singleAsImmutable()
  #else
    #multipleAsImmutable()
  #end
#end

#macro singleAsImmutable()
  #set (attribute = attributes.iterator.next)
        public {classname} asImmutable() {
            assertIsValid();

            return new {classname}({immutableStateAttribute()});
        }
#end

#macro multipleAsImmutable()
        public {classname} asImmutable() {
            assertIsValid();

            return new {classname}(
  #set (iterator = attributes.iterator)
  #while (iterator.hasNext)
    #set (attribute = iterator.next)
    #if (iterator.hasNext)
                    {immutableStateAttribute()},
    #else
                    {immutableStateAttribute()}
            );
    #end
  #end
        }
#end


#macro mapConverters(type)
  #if (type.isState)
, toMap##
  #else if (type.isMap)
, toMutableMap{mapConverters(type.mapType.entry)}##
  #else if (type.isList)
, toMutableList{mapConverters(type.collectionType.type)}##
  #else if (type.isSet)
, toMutableSet{mapConverters(type.collectionType.type)}##
  #end
#end

#macro mapStateAttribute()
  #if (attribute.type.isMap)
asMutableMap({attribute.name}{mapConverters(attribute.type.mapType.entry)})##
  #else if (attribute.type.isList)
asMutableList({attribute.name}{mapConverters(attribute.type.collectionType.type)})##
  #else if (attribute.type.isSet)
asMutableSet({attribute.name}{mapConverters(attribute.type.collectionType.type)})##
  #else if (attribute.type.isState)
{attribute.name} != null ? {attribute.name}.asMap() : null##
  #else
{attribute.name}##
  #end
#end

#macro asMap()

  #set (convertPrimitives = false)
        public Map asMap() {
            Map result = new LinkedHashMap();

  #foreach (attribute in attributes)
            result.put("{attribute.name}", {mapStateAttribute()});
  #end

            return result;
        }
#end

#macro asStringState()
  #set (converter = suffix("converter"))

        public {stringClass} asStringState() {
            return asStringState(new {stringConverterClass}());
        }

        public {stringClass} asStringState(StringStateConverter {converter}) {
            return asStringState(new {stringConverterClass}({converter}));
        }

  #set (convertPrimitives = true)
  #if (attributes.size == 1)
    #singleAsStringState()
  #else
    #multipleAsStringState()
  #end
#end

#macro singleAsStringState()
  #set (attribute = attributes.iterator.next)
        public {stringClass} asStringState({stringConverterClass} {converter}) {
            return new {stringClass}({converter}.{attribute.name}ToString({attribute.name}));
        }
#end

#macro multipleAsStringState()
        public {stringClass} asStringState({stringConverterClass} {converter}) {
            return new {stringClass}(
  #set (iterator = attributes.iterator)
  #while (iterator.hasNext)
    #set (attribute = iterator.next)
    #if (iterator.hasNext)
                    {converter}.{attribute.name}ToString({attribute.name}),
    #else
                    {converter}.{attribute.name}ToString({attribute.name})
            );
    #end
  #end
        }
#end

#macro mutableState()
  #set ($.attributes = state.mutableAttributes)
  #set (tab = "        ")

  #classHeader()
  #attributes(tab, "")
  #emptyConstructor(mutableClass)
  #constructor(false, mutableClass, false, tab)
  #copyConstructor()
  #getters(tab)
  #setters()
  #withMethods()
  #asImmutable()
  #asMap()
  #asStringState()
  #isValid()
  #rootValidate()
  #validate()
  #assertIsValid()
  #hashCode(tab)
  #equals(mutableClass, tab)
  #hashCodeValue(tab)
  #equalsValue(mutableClass, tab)
  #toString(tab)
    }
#end
